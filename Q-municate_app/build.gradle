buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.+'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

repositories {
    maven { url 'https://dl.bintray.com/drummer-aidan/maven' }
    maven { url "https://jitpack.io" }
    maven { url 'https://maven.fabric.io/public' }
}

android {
    compileSdkVersion rootProject.compileSdkVersion
    buildToolsVersion rootProject.buildToolsVersion

    defaultConfig {
        minSdkVersion rootProject.minSdkVersion
        targetSdkVersion rootProject.targetSdkVersion
        versionCode rootProject.versionCode
        versionName rootProject.versionName
        multiDexEnabled = true
    }

//     before release need to enable Crashlytics

    signingConfigs {
        release {
            storeFile file(property('prodStoreFile'))
            storePassword property('prodStorePassword')
            keyAlias property('prodKeyAlias')
            keyPassword property('prodKeyPassword')
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            debuggable false
            ext.enableCrashlytics = true
            signingConfig signingConfigs.release
            buildConfigField "String", "APP_ID", property('prodAppID')
            buildConfigField "String", "AUTH_KEY", property('prodAuthKey')
            buildConfigField "String", "AUTH_SECRET", property('prodAuthSecret')
            buildConfigField "String", "ACCOUNT_KEY", property('prodAccountKey')
            buildConfigField "String", "PUSH_REGISTRATION_APP_ID", property('prodPushRegistrationAppID')
            buildConfigField "String", "TWITTER_KEY", property('prodTwitterDigitsConsumerKey')
            buildConfigField "String", "TWITTER_SECRET", property('prodTwitterDigitsConsumerSecret')
            resValue "string", "twitterDigitsApiKey", property('prodTwittedDigitsApiKey')
            resValue "string", "facebookAppId", property('prodFacebookAppID')
        }

        debug {
            debuggable true
            ext.enableCrashlytics = false
            versionNameSuffix ' (debug)'
            buildConfigField "String", "APP_ID", property('devAppID')
            buildConfigField "String", "AUTH_KEY", property('devAuthKey')
            buildConfigField "String", "AUTH_SECRET", property('devAuthSecret')
            buildConfigField "String", "ACCOUNT_KEY", property('devAccountKey')
            buildConfigField "String", "PUSH_REGISTRATION_APP_ID", property('devPushRegistrationAppID')
            buildConfigField "String", "TWITTER_KEY", property('devTwitterDigitsConsumerKey')
            buildConfigField "String", "TWITTER_SECRET", property('devTwitterDigitsConsumerSecret')
            resValue "string", "twitterDigitsApiKey", property('devTwittedDigitsApiKey')
            resValue "string", "facebookAppId", property('devFacebookAppID')
        }
    }

    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }

    dexOptions {
        javaMaxHeapSize "4g"
    }

    lintOptions {
        abortOnError false
    }

    applicationVariants.all { variant ->
        changeApkFileName(variant)
    }
}

dependencies {
    compile project(':Q-municate_core')

    compile "com.android.support:multidex:${rootProject.multiDexVersion}"
    compile "com.android.support:appcompat-v7:${rootProject.supportVersion}"
    compile "com.android.support:design:${rootProject.supportVersion}"
    compile "com.android.support:recyclerview-v7:${rootProject.supportVersion}"
    compile "com.google.android.gms:play-services:${rootProject.googlePlayServicesVersion}"

    compile "com.nostra13.universalimageloader:universal-image-loader:${rootProject.universalImageLoaderVersion}"
    compile 'com.flurry.android:analytics:6.2.0'
    compile "com.jakewharton:butterknife:${rootProject.butterknifeVersion}"
    compile "com.afollestad:material-dialogs:${rootProject.materialDialogsVersion}"
    compile "com.github.orangegangsters:swipy:${rootProject.swipyRecyclerviewVersion}@aar"
    compile "com.timehop.stickyheadersrecyclerview:library:${rootProject.stickylistheadersRecyclerViewVersion}@aar"
    compile "com.rockerhieu.emojicon:library:${rootProject.emojiconVersion}"
    compile "com.soundcloud.android:android-crop:${rootProject.cropVersion}@aar"
    compile('com.android.support:cardview-v7:23.2.0') {
        force = true
    }
    compile('com.crashlytics.sdk.android:crashlytics:2.6.2@aar') {
        transitive = true;
    }
}

def changeApkFileName(variant) {
    variant.outputs.each { output ->
        def file = output.outputFile
        def newName = 'QMUN_Android_v.' + rootProject.versionName + '_c.' + rootProject.versionCode + '-' + variant.buildType.name + '.apk'
        output.outputFile = new File(file.parent, newName)
    }
}